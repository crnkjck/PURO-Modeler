PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX puro: <http://lod2-dev.vse.cz/ontology/puro#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX miso: <http://temporary.url/miso#>


INSERT
{   
  GRAPH <http://owlfragmentA> {
    ?d puro:transformedTo ?domainClass .
    ?r puro:transformedTo ?rangeClass .
    ?link puro:transformedTo ?property .
  
     ?domainClass a owl:Class .
     ?rangeClass a owl:Class .
     ?misoInstance a ?rangeClass;
      a miso:SomeInstances.
     ?property a owl:ObjectProperty;
      rdfs:range ?rangeClass;
      rdfs:range miso:SomeInstances;
      rdfs:domain ?domainClass .
  }   
}
WHERE
{ 
  ?d a puro:BRelation;
   rdfs:label ?d_label;
   puro:linkedTo ?link .
  
  ?link a puro:Link;
    rdfs:label ?link_label;
    puro:linkedTo ?b.
  ?b a puro:Some_objects; 
    puro:instanceOf ?miso_type .
  ?r a puro:BType;
   rdfs:label ?r_label . 
  ?miso_type a puro:BType ;
      rdfs:label ?miso_type_label ;
      puro:instanceOf ?r .
   
   FILTER ( EXISTS {?b puro:modelingStyle puro:classMembership} 
    || EXISTS {?b puro:modelingStyle puro:objectPropSubclass} 
    || EXISTS {?b puro:modelingStyle puro:objectPropAll} 
    || EXISTS {?b puro:modelingStyle puro:reify} ) 
    
    
   OPTIONAL {GRAPH <http://owlfragmentA> {?r puro:transformedTo ?transformedTypeClass.}}
   OPTIONAL {?d puro:mappedTo ?d_mapping .}
   OPTIONAL {?r puro:mappedTo ?r_mapping .}
   
  BIND(COALESCE(?d_mapping, URI(CONCAT("http://owlfragment/entity/", UCASE(SUBSTR(?d_label,1,1)), SUBSTR(?d_label,2), "_Relation" ))) AS ?domainClass )
  BIND(IF(?link_label != "", URI(CONCAT("http://owlfragment/entity/", ?link_label)), 
    URI(CONCAT("http://owlfragment/entity/property_", ?d_label))) AS ?property )
  BIND(COALESCE(?transformedTypeClass, ?r_mapping, URI(CONCAT("http://owlfragment/entity/", ?r_label))) AS ?rangeClass )       
  BIND(URI(CONCAT("http://owlfragment/entity/", LCASE(SUBSTR(?r_label,1,1)), SUBSTR(?r_label,2) )) AS ?rangeProperty ) 
  BIND(URI(CONCAT("http://owlfragment/entity/", LCASE(SUBSTR(?d_label,1,1)), SUBSTR(?d_label,2) )) AS ?domainProperty ) 
  BIND(URI(CONCAT("http://owlfragment/entity/", UCASE(SUBSTR(?link_label,1,1)), SUBSTR(?link_label,2) )) AS ?propertyClass ) 
  BIND(URI(CONCAT("http://owlfragment/entity/", ?miso_type_label )) AS ?misoInstance ) 
   FILTER NOT EXISTS {
      GRAPH ?any {?link puro:transformedTo [] }.      
   }
}